reset
set encoding utf8 # mandatory


set tics font "Helvetica,18"
set font "Helvetica,26"

LCases="abcdefghijklmnopqrstuvwxyz"
SCases="ᴀʙᴄᴅᴇғɢʜɪᴊᴋʟᴍɴᴏᴘǫʀsᴛᴜᴠᴡxʏᴢ"

toscchr(c)= c eq ""  ?  ""  :  substr( SCases.c, strstrt(LCases.c, c), strstrt(LCases.c, c) )

texsc(s) = strlen(s) <= 1 ? toscchr(s) : texsc(s[1:strlen(s)/2]).texsc(s[(strlen(s)/2)+1:strlen(s)])

sal_label="Salzinnes"
ein_label="Einsiedeln"
dib14_label="Dibco2014"
dib16_label="Dibco2016"
phi_label="Phi"
palm_label="Palm"

sal_label=texsc(sal_label)
ein_label=texsc(ein_label)
dib14_label=texsc(dib14_label)
dib16_label=texsc(dib16_label)
phi_label=texsc(phi_label)
palm_label=texsc(palm_label)

set key font ",17"
set key spacing 0.725

set terminal pdf
set output "plot_diffF1corr_full.pdf"

set multiplot


#set logscale y
set size 1, 1
set yrange [0:4.0]
set xrange [-0.4:1.1]
set xlabel "Correlation coefficient" font "Helvetica,22"
set grid

set key box right top

#
# define line styles using explicit rgbcolor names
#

set style func linespoints

set style line 1 lt 5 lc rgb "#28BAFC" lw 2 dt 4 pt 5 ps .65
set style line 2 lt 5 lc rgb "#28BAFC" lw 2 dt 4 pt 9 ps .85

set style line 3 lt 5 lc rgb "#D99A23" lw 2 dt 4 pt 2 ps .75
set style line 4 lt 5 lc rgb "#D99A23" lw 2 dt 4 pt 3 ps .85

set style line 5 lt 5 lc rgb "#FA4324" lw 2 dt 4 pt 4 ps .75
set style line 6 lt 5 lc rgb "#FA4324" lw 2 dt 4 pt 8 ps .65



set rmargin 5
#set ylabel '{/Helvetica DANN-SAE F@_{{/*0.05 &{c}}1}} (%)'
#set ylabel '{/Helvetica DANN/SAE}'
set ylabel '{/Helvetica F@_{{/*0.05 &{c}}1}^{/Helvetica {/*0.1 &{c}}Bin-DANN}} / {/Helvetica F@_{{/*0.05 &{c}}1}^{/Helvetica {/*0.1 &{c}}SAE}} ' font "Helvetica,22"
set title ""


set origin 0,0
set size 1,1


set arrow 6 from -0.4,1 to 1.1,1 nohead dt "." lw 2


plot \
        'data-full-difff1-corr.txt' using 1:2 with point ls 1 title sal_label,\
        'data-full-difff1-corr.txt' using 3:4 with point ls 2 title ein_label,\
        'data-full-difff1-corr.txt' using 5:6 with point ls 3 title dib14_label,\
        'data-full-difff1-corr.txt' using 7:8 with point ls 4 title dib16_label,\
        'data-full-difff1-corr.txt' using 9:10 with point ls 5 title phi_label,\
        'data-full-difff1-corr.txt' using 11:12 with point ls 6 title palm_label
        



