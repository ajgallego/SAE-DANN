reset
set encoding utf8 # mandatory


set tics font "Helvetica,18"
set font "Helvetica,26"

LCases="abcdefghijklmnopqrstuvwxyz"
SCases="ᴀʙᴄᴅᴇғɢʜɪᴊᴋʟᴍɴᴏᴘǫʀsᴛᴜᴠᴡxʏᴢ"

toscchr(c)= c eq ""  ?  ""  :  substr( SCases.c, strstrt(LCases.c, c), strstrt(LCases.c, c) )

texsc(s) = strlen(s) <= 1 ? toscchr(s) : texsc(s[1:strlen(s)/2]).texsc(s[(strlen(s)/2)+1:strlen(s)])

sal_label="Salzinnes"
dib_label="Dibco2016"
phi_label="Phi"

sal_label=texsc(sal_label)
dib_label=texsc(dib_label)
phi_label=texsc(phi_label)

source_sal_text = "Salzinnes-"
source_dib_text = "Dibco2016-"
source_phi_text = "Phi-"

source_sal_text=texsc(source_sal_text)
source_dib_text=texsc(source_dib_text)
source_phi_text=texsc(source_phi_text)


set key font ",17"
set key spacing 0.735

set terminal pdf
set output "plot_diffF1corr.pdf"

set multiplot


#set logscale y
set size 1, 1
set yrange [0:4.0]
set xrange [-0.2:1.1]
set xlabel "Correlation coefficient" font "Helvetica,22"
set grid

set key box right top

#
# define line styles using explicit rgbcolor names
#

set style func linespoints

set style line 1 lt 5 lc rgb "#28BAFC" lw 2 dt 4 pt 5 ps .75
set style line 2 lt 5 lc rgb "#28BAFC" lw 2 dt 4 pt 9 ps .85

set style line 3 lt 5 lc rgb "#D99A23" lw 2 dt 4 pt 7 ps .75
set style line 4 lt 5 lc rgb "#D99A23" lw 2 dt 4 pt 9 ps .85

set style line 5 lt 5 lc rgb "#FA4324" lw 2 dt 4 pt 7 ps .75
set style line 6 lt 5 lc rgb "#FA4324" lw 2 dt 4 pt 5 ps .75



set rmargin 5
#set ylabel '{/Helvetica DANN-SAE F@_{{/*0.05 &{c}}1}} (%)'
#set ylabel '{/Helvetica DANN/SAE}'
set ylabel '{/Helvetica F@_{{/*0.05 &{c}}1}^{/Helvetica {/*0.1 &{c}}Bin-DANN}} / {/Helvetica F@_{{/*0.05 &{c}}1}^{/Helvetica {/*0.1 &{c}}SAE}} ' font "Helvetica,22"
set title ""


set origin 0,0
set size 1,1

#set object 1 rect from 16,88.2 to 22,85.5 back dt "."
#set object 1 rect fc rgb "cyan" fillstyle empty

#set arrow 1 from 22,85.5 to 58,85.1 nohead dt "." 
#set arrow 2 from 22,88.2 to 58,88.8 nohead dt "." 





# Implements a 'textsc' function like in (La)TeX.
# Based on original answer at https://stackoverflow.com/a/54174759/11369382




set arrow 1 from 0.6075,3.275 to 1.077,3.275 nohead lc rgb "purple" lt 1
set arrow 2 from 0.6075,2.65 to 1.077,2.65 nohead lc rgb "purple" lw 1

set label 11 source_sal_text at 0.77, 3.82 left nopoint tc rgb "#28BAFC" font ",17.65"
set label 12 source_dib_text at 0.77, 3.18 left nopoint tc rgb "#D99A23" font ",17.65"
set label 13 source_phi_text at 0.77, 2.55 left nopoint tc rgb "#FA4324" font ",17.65"

set arrow 6 from -0.2,1 to 1.1,1 nohead dt "." lw 2


plot \
        "+" u 1:(NaN) title " " w dots linecolor rgb "white",\
        'data-difff1-corr.txt' using 2:1 with point ls 1 title dib_label,\
        'data-difff1-corr.txt' using 4:3 with point ls 2 title phi_label,\
        "+" u 1:(NaN) title " " w dots linecolor rgb "white",\
        'data-difff1-corr.txt' using 6:5 with point ls 3 title sal_label,\
        'data-difff1-corr.txt' using 8:7 with point ls 4 title phi_label,\
        "+" u 1:(NaN) title " " w dots linecolor rgb "white",\
        'data-difff1-corr.txt' using 10:9 with point ls 5 title sal_label,\
        'data-difff1-corr.txt' using 12:11 with point ls 6 title dib_label
        



